/**
 * ROSACE case study, from:
 * 
 * Claire Pagettiâ€€, David SaussiÃ©y, Romain Gratiaâ€€, Eric Noulardâ€€, Pierre Siron,
 * "The ROSACE Case Study: From Simulink Specification to Multi/Many-Core Execution,"
 * RTAS (2014).
 * 
 * This implementation is based on code from:
 * 
 * Deschamps, Henrick and Cappello, Gerlando and Cardoso, Janette and Siron, Pierre
 * Coincidence Problem in CPS Simulations: the R-ROSACE Case Study.
 * (2018) In: 9th European Congress Embedded Real Time Software and Systems ERTS2 2018,
 * 31 January 2018 - 2 February 2018 (Toulouse, France).
 * https://www.erts2018.org/authors_detail_inverted_Cappello%20Gerlando.html
 * 
 * The code was download from https://svn.onera.fr/schedmcore/branches/ROSACE_CaseStudy/.
 * 
 * This program uses a forward Euler simulation of aircraft dynamics and implements
 * throttle and elevator control. The parameters specified execute an elevation
 * climb from an initial 10,000 feet to a target 11,000 feet.
 * 
 * The style of execution is that each component has a `period` parameter that
 * determines the frequency at which it runs. The program relies on persistence
 * of inputs in the C target and on the dependency analysis of the *uses* field
 * of reactor signature.
 * 
 * To run this, you could ideally uncomment the `build` attribute of the target
 * declaration, and everything would be taken care of. But for reasons I cannot
 * fathom, `lfc` cannot find the shell file referred to in that attribute.
 * So for now, you have to do it by hand:
 * 
 * ```
 * cd examples-lingua-franca/C
 * lfc src/periodic/Rosace.lf
 * bin/Rosace
 * gnuplot src/avionics/rosace.gnuplot
 * open rosace.pdf
 * ```
 * 
 * You should see a smooth climb from 10,000 feet to 11,000 feet.
 * You can experiment, for example, with the period with which the filters
 * sample the sensor outputs from the aircraft model. If you change the
 * `filter_period` parameter of the `RosaceController` from its default
 * 10 ms to 100 ms, for example, you will far worse behavior from the
 * aircraft.
 * 
 * @author Edward A. Lee
 */

target C {
    fast: true,
    // build: "./build_run_plot.sh Rosace",   // FIXME: Doesn't find it.
    timeout: 10 min
}

import Aircraft from "AircraftSimulator.lf"
import RosaceController from "RosaceController.lf"
import PrintToFile from "../lib/PrintToFile.lf"

preamble {=
    // Shared constants.
    #define delta_th_eq (1.5868660794926)
    #define delta_e_eq (0.012009615652468)    
=}

// Documentation
@icon("Variables.png")
reactor Variables {}

reactor Command(
    period: time(100 ms),
    value: double(0.0)
) {
    timer t(0, period)
    output c:double
    reaction(t) -> c {=
        lf_set(c, self->value);
    =}
}

main reactor(filter_period: time(100 ms)) {
    variables = new Variables()
    a = new Aircraft()
    c = new RosaceController()
    altitude = new Command(value = 11000) // Altitude command
    speed = new Command(value = 0.0)      // Delta airspeed from nominal Va_eq (230)
    
    p_h = new PrintToFile(filename = "altitude.data")
    p_Va = new PrintToFile(filename = "airspeed.data")
        
    a.h, a.az, a.Vz, a.q, a.Va -> c.h, c.az, c.Vz, c.q, c.Va
    altitude.c -> c.c    
    speed.c -> c.s
    
    c.delta_ec -> a.delta_ec
    c.delta_thc -> a.delta_thc
    
    // Print connections.
    a.h -> p_h.y
    a.Va -> p_Va.y
}
