/**
 * This program is a simple chat application for N users.
 *
 * @author Byeonggil Jun (junbg@hanyang.ac.kr)
 * @author Hokeun Kim (hokeunkim@berkeley.edu)
 */
target C {
    keepalive: true,   
    coordination-options: {advance-message-interval: 10 msec} 
};

preamble {=
        #include <unistd.h>
=}

reactor InputHandler {
    preamble {=
        // Global buffer.
        char buf[256];
        void* read_input(void* response) {
            int c;
            int i = 0;
            while(1) {
                printf("Press Enter a message.\n");  
                while((c = getchar()) != '\n') {
                    buf[i++] = c;
                    if (c == EOF) {
                        request_stop();
                        break;
                    }
                }
                buf[i] = 0;
                printf("User input: %s\n", buf);
                i = 0;

                char* ptr = buf;
                // The following copies the char*, not the string.
                lf_schedule_copy(response, 0, &ptr, 1);
                if (c == EOF) {
                    break;
                }
            }
            return NULL;
        }
    =}

    physical action response:string;
    output out:string;
    
    reaction(startup) -> response {=
        lf_thread_t thread_id;
        lf_thread_create(&thread_id, &read_input, response);
    =}

    reaction(response) -> out {=
        printf("Reacting to physical action at %lld\n", get_elapsed_logical_time());
        lf_set(out, response->value);
    =}
}

reactor Printer {
    //add N input control
    input in:string;
    input sender:int;

    reaction(in, sender) {=
        printf("Received message from %d: %s\n", sender->value, in->value);
        if (sender->is_present) {
            printf("sender has no problem, value = %d\n", sender);
        }
    =}
}

reactor ChatHandler (
    N:int(2)
) {
    //add N input control
    input[N] receive:string;
    output send:string;
    u = new InputHandler();
    r = new Printer();

    reaction(u.out) -> send {=
        lf_set(send, u.out->value);
    =}

    reaction(receive) -> r.sender, r.in {=
        for (int i = 0; i < receive_width; i++) {
            if (receive[i]->is_present) {
                lf_set(r.sender, i);
                printf("send with sender name %d\n", i); 
                lf_set(r.in, receive[i]->value); 
            }
        }
    =}
}

federated reactor SimpleChatN_with_sender (
    N:int(2)
) {
    people = new[N] ChatHandler(N=N);
    (people.send)+ -> people.receive
}
