/**
 * This variant eliminates the unnecessary delay in the previous example by setting the STA to 0
 * until the next time that an input is expected. The ability to set the STA dynamically using
 * knowledge about the program is a powerful feature of the decentralized coordination model.
 *
 * @author Edward A. Lee
 */
target C {
  coordination: decentralized,
  timeout: 1 s,
  workers: 8
}

reactor Upstream {
  output out: int
  timer t(0, 100 ms)
  state counter: int = 0

  reaction(t) -> out {=
    self->counter++;
    lf_sleep(MSEC(10));
    lf_set(out, self->counter);
    lf_print("Upstream " PRINTF_TIME ": Sensing... (counter=%d)",
        lf_time_logical_elapsed(), self->counter);
  =}
}

@label("STA == variable, STAA == 0")
reactor Downstream(STA: time = 20 ms) {
  input in: int
  timer t(0, 10 ms)
  state count: int = 0

  reaction(in) {=
    lf_print("Downstream " PRINTF_TAG ": Received (in=%d) at physical time " PRINTF_TIME,
        lf_time_logical_elapsed(), lf_tag().microstep, in->value, lf_time_physical_elapsed());
    // Temporarily set STP offset to 0 to avoid the delay in the timer reaction.
    lf_set_sta(0);
    self->count = 0;
  =} STAA(0) {=
    lf_print("Downstream " PRINTF_TAG ": *** STAA violation on data (in=%d) at physical time " PRINTF_TIME,
        lf_time_logical_elapsed(), lf_tag().microstep, in->value, lf_time_physical_elapsed());
  =}

  reaction(t) {=
    lf_print("Downstream " PRINTF_TAG ": React to timer at physical time " PRINTF_TIME,
        lf_time_logical_elapsed(), lf_tag().microstep, lf_time_physical_elapsed());
    self->count++;
    if (self->count == 10) {
      lf_set_sta(MSEC(20));
    }
  =}
}

federated reactor {
  s = new Upstream()
  @label("STA == STAA == 0")
  a = new Downstream()

  s.out -> a.in
}
