/**
 * Demo of a use of WebSocketServer enabling a user interface realized in the browser. Compile and
 * run this program, then open WebSocket.html in your favorite browser. This example program sends
 * to the web page a counting sequence. It also accepts text messages from the web page and prints
 * them on standard output.
 *
 * This example also shows how to limit the number of connections to just two. If you try to open
 * the WebSocket.html web page more than twice, only the first two attempts will succeed in
 * connecting. By default, WebSocketServer imposes no such limit.
 *
 * In this example, if you connect two clients, one will receive even numbered counts,
 * and the other will receive odd numbered counts. This is because the count state variable
 * is shared across all clients.
 *
 * @author Edward A. Lee
 */
target C {
  keepalive: true
}

import WebSocketServer from "../lib/WebSocketServer.lf"

main reactor {
  state count: int = 0
  logical action send_action: web_socket_instance_t*

  s = new WebSocketServer(max_clients=2)  // Limit number of clients to 2.

  reaction(startup) {=
    lf_print("======== Starting server. Open WebSocket.html in your favorite browser.");
  =}

  reaction(send_action) -> s.send, send_action {=
    char* message;
    asprintf(&message, "Count is: %d", self->count++);

    web_socket_message_t* container = (web_socket_message_t*)malloc(sizeof(web_socket_message_t));
    container->message = message;
    container->length = strlen(message) + 1;
    container->wsi = send_action->value->wsi;

    lf_set(s.send, container);
    // Schedule the next send.
    lf_schedule_token(send_action, SEC(1), send_action->token);
  =}

  reaction(s.connected) -> send_action {=
    if (s.connected->value.connected) {
      lf_print("======== Connected a new client");

      // Start sending.
      lf_schedule_copy(send_action, 0, &s.connected->value, 1);
    } else {
      lf_print("======== Disconnected client");
    }
  =}

  reaction(s.received) {=
    // NOTE: The WebSocketServer ensures that the received message is null terminated.
    lf_print("======== Application received: %s", (char*)s.received->value->message);
  =}
}
