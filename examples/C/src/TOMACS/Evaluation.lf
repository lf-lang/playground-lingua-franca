target C {
  timeout: 1 sec,
  // tracing: true,
  coordination: centralized
}

reactor A (offset:time = 0, period:time = 100 msec){
  timer t(offset, period)
  input in: int
  output out: int

  reaction (t) -> out {=
    lf_set(out, 42);
  =}

  reaction(in) {=
    instant_t logical_time = lf_time_logical_elapsed();
    instant_t physical_time = lf_time_physical_elapsed();

    lf_print("The lag is " PRINTF_TIME ".", physical_time - logical_time);
  =}
}

reactor B {
  input in: int
  output out: int
  
  reaction(in) -> out {=
    lf_set(out, in->value);
  =}
}

federated reactor at localhost {
  a = new A(offset = 0, period = 10 msec)
  b = new B()

  a.out -> b.in
  b.out -> a.in
  // Replace preceeding line with the following to observe overhead introduced by coordinating
  // b.out -> a.in after 0
}