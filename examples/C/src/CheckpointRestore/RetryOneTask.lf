/**
 * This project implements a fault-tolerant task execution model using checkpoint and retry
 * mechanisms, and predictive task dropping. The system is designed to simulate execution of
 * multi-segment tasks with faults using logical execution time.
 *
 * 1. Checkpointed Task Execution: Tasks are divided into segments, each with its own worst-case and
 * optimistic execution time. Faults are simulated with probabilistic failures, and task is restored
 * from the checkpoint when fails.
 *
 * 2. Predictive Task Dropping: Upon segment failure, the system evaluates whether retrying will
 * still meet the task's deadline and only proceeds if the deadline constraint is preserved.
 *
 * The purpose of this design is to investigate how logic-based scheduling and retry policies can
 * improve reliability in real-time embedded systems.
 *
 * @author Dongha Kim
 */
target C {
  timeout: 9900 msec
}

preamble {=
  #include <stdlib.h> // rand()
  #include "platform.h" // lf_sleep()

  typedef struct task_info_t {
    uint16_t task_num;
    uint16_t dead_line;
    uint16_t num_of_segs;
    uint16_t* wcet;
    uint16_t* optwcet;
  } task_info_t;
=}

reactor TaskBody {
  preamble {=
    #define TASK1_SLEEP_MSEC  200
    #define TASK2_SLEEP_MSEC  200
    #define TASK3_SLEEP_MSEC  200
    #define FAILURE_RATE 80 // 80% chance

    int seg1() {
      lf_sleep(MSEC(TASK1_SLEEP_MSEC));
      if (rand() % 100 < FAILURE_RATE) {
        return 1;
      }
      return 0;
    }
    int seg2() {
      lf_sleep(MSEC(TASK2_SLEEP_MSEC));
      if (rand() % 100 < FAILURE_RATE) {
        return 2;
      }
      return 0;
    }
    int seg3() {
      lf_sleep(MSEC(TASK3_SLEEP_MSEC));
      if (rand() % 100 < FAILURE_RATE) {
        return 3;
      }
      return 0;
    }
    int total_task(int checkpoint) {
      int ret = -1;
      if (checkpoint <= 1) {  // Start from task1 if no checkpoint
        ret = seg1();
        if (ret != 0) {
          return ret; // Return failure checkpoint
        }
      }

      if (checkpoint <= 2) {  // Start from task2 if the previous step succeeded
        ret = seg2();
        if (ret != 0){
          return ret; // Return failure checkpoint
        }
      }
      if (checkpoint <= 3) {  // Start from task2 if the previous step succeeded
        ret = seg3();
        if (ret != 0){
          return ret; // Return failure checkpoint
        }
      }
      return 0;
    }
  =}

  input seg_start_num: int
  output failed_seg_num: int

  reaction(seg_start_num) -> failed_seg_num {=
    lf_set(failed_seg_num, total_task(seg_start_num->value));
  =}
}

reactor Task(
    task_num: uint16_t = 0,
    dead_line: uint16_t = 0,
    num_of_segs: uint16_t = 0,
    wcet: uint16_t[] = {},
    optwcet: uint16_t[] = {}) {
  input new_instance: int
  input in: int
  logical action advance_lt: int
  output fail: int
  output instance_start_time: time
  output task_info_update: task_info_t
  state task_info: task_info_t
  state seg_start_num: int

  task_body = new TaskBody()

  reaction(startup) -> task_info_update {=
    self->task_info.task_num = self->task_num;
    self->task_info.num_of_segs = self->num_of_segs;
    self->task_info.wcet = self->wcet;
    self->task_info.optwcet = self->optwcet;
    self->task_info.dead_line = self->dead_line;
    lf_set(task_info_update, self->task_info);
  =}

  // Setup the starting segment and update the instance start time to the retry reactor.
  reaction(new_instance, in) -> task_body.seg_start_num, instance_start_time {=
    if(in->is_present) {
      // When Retry was triggered. in->value should not be 0.
      self->seg_start_num = in->value;
    } else {
      // When timer is triggering the next instance.
      self->seg_start_num = 1; // Start from segment 1.
      lf_set(instance_start_time, lf_time_logical_elapsed());
      // When timer triggers.
      lf_print("**** New instance of task %d starting. Current Logical time: %lld msecs, microstep: %d, Physical time: %lld msecs.\n", self->task_num, lf_time_logical_elapsed() / MSEC(1), lf_tag().microstep, lf_time_physical_elapsed() / MSEC(1));
    }
    lf_set(task_body.seg_start_num, self->seg_start_num);
  =}

  // Calculate the number of successed segments. If failed_seg_num=3 and in self->seg_start_num=1, 2 segments success.
  reaction(task_body.failed_seg_num) -> advance_lt {=
    // Calculate the number of successed segments. If task_body.failed_seg_num=3 and in self->seg_start_num=1, 2 segments success.
    int num_of_success = 0;
    num_of_success = task_body.failed_seg_num->value - self->seg_start_num;
      if (num_of_success < 0) {
        // This means task_body.failed_seg_num->value was 0 and successed to end of task. Recalculate the number of successed segments.
        num_of_success +=  self->task_info.num_of_segs + 1;
      }

      // Schedule logical action to advance time.
      lf_print("TASK%d: Current Logical time: %lld msecs, microstep: %d, Physical time: %lld msecs.", self->task_num, lf_time_logical_elapsed() / MSEC(1), lf_tag().microstep, lf_time_physical_elapsed() / MSEC(1));

      // Caluculate the time of the total successed segments.
      int total_success_time = 0; // msecs
      for (int i=0; i < num_of_success; i++){
        total_success_time += self->task_info.optwcet[self->seg_start_num + i -1];
      }

      if(task_body.failed_seg_num->value == 0) {
        // Task is finished. Advance as much as the sucessed segments.
        lf_schedule_int(advance_lt, total_success_time * MSEC(1), 0);
        lf_print("TASK%d: All segments finished. %d task(s) success. Advancing logical time by %d msecs. \n", self->task_num, num_of_success, total_success_time);
      } else {
        // Segment failed. Advance as much as successed segments + 1 failed segment.
        lf_schedule_int(advance_lt, total_success_time * MSEC(1) + self->task_info.wcet[task_body.failed_seg_num->value - 1] * MSEC(1), task_body.failed_seg_num->value);
        lf_print("TASK%d: Segment %d failed. %d task(s) success. Advancing logical time by %d msecs. \n", self->task_num, task_body.failed_seg_num->value, num_of_success, total_success_time + self->task_info.wcet[task_body.failed_seg_num->value - 1]);
      }
  =}

  reaction(advance_lt) -> fail {=
    lf_set(fail, advance_lt->value);
  =}
}

// Naming? Timely retry?
reactor CheckDeadlineAndRetry {
  input instance_start_time_update: time
  input fail: int
  output out: int
  state instance_start_time: time
  input task_info_update: task_info_t
  state task_info: task_info_t

  reaction(task_info_update) {=
    self->task_info.task_num = task_info_update->value.task_num;
    self->task_info.num_of_segs = task_info_update->value.num_of_segs;
    self->task_info.wcet = task_info_update->value.wcet;
    self->task_info.optwcet = task_info_update->value.optwcet;
    self->task_info.dead_line = task_info_update->value.dead_line;
  =}

  reaction(instance_start_time_update) {=
    // Update the instance start time. This can also be implemented by a message by the Task reactor.
    self->instance_start_time = lf_time_logical_elapsed();
  =}

  reaction(fail) -> out {=
    if(fail->value == 0) {
      // 0 means, all segments are finished. Don't trigger the task again, so do nothing.
    } else {
      // Check deadline in logical time. Does not schedule next retry, when deadline violation.

      // Caluculate the additional time required to finish task.
      int total_success_time = 0; // msecs
      for (int i=fail->value; i <=  self->task_info.num_of_segs; i++){
        total_success_time += self->task_info.optwcet[i - 1];
      }

      instant_t min_instance_finish_time = lf_time_logical_elapsed() + total_success_time * MSEC(1);
      if(min_instance_finish_time > self->instance_start_time + (self->task_info.dead_line * MSEC(1))) {
        lf_print("TASK%d: Current Logical time: %lld msecs, microstep: %d, Physical time: %lld msecs.", self->task_info.task_num, lf_time_logical_elapsed() / MSEC(1), lf_tag().microstep, lf_time_physical_elapsed() / MSEC(1));
        lf_print("TASK%d: ****Logical time deadline violation predicted.\nDeadline is: %lld msecs, while minimum predicted instance finish time is %lld msecs.\nDropping left over tasks, and giving up instance.\n", self->task_info.task_num, (self->task_info.dead_line * MSEC(1) + self->instance_start_time)/ MSEC(1), min_instance_finish_time / MSEC(1));
        // *****This else if tests when there is no deadline prediction!
        // Don't erase this part.
        // } else if(self->task_info.dead_line * MSEC(1) < lf_time_logical_elapsed() - self->instance_start_time) {
      //   lf_print("\nLogical time deadline violation detected on Logical time: %lld msecs, microstep: %d, Physical time: %lld msecs.\nDeadline was: %lld msecs.\n", lf_time_logical_elapsed() / MSEC(1), lf_tag().microstep, lf_time_physical_elapsed() / MSEC(1), (self->task_info.dead_line * MSEC(1) + self->instance_start_time)/ MSEC(1));
      } else {
        // Did not violate predicted deadline.
        lf_set(out, fail->value);
      }
    }
  =}
}

reactor TaskScheduler(period: time = 0 msec) {
  timer t(0, period)
  output out: int

  reaction(startup) {=
    srand(time(NULL));
  =}

  reaction(t) -> out {=
    lf_set(out, 0);
  =}
}

main reactor {
  task = new Task(
      task_num=1,
      dead_line=3000,
      num_of_segs=3,
      wcet = {300, 300, 300},
      optwcet = {250, 250, 250})
  retry = new CheckDeadlineAndRetry()
  scheduler = new TaskScheduler(period = 5000 msec)

  scheduler.out -> task.new_instance
  task.task_info_update -> retry.task_info_update
  task.instance_start_time -> retry.instance_start_time_update
  task.fail -> retry.fail
  retry.out -> task.in after 0
}
